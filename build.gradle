import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '3.2.2' apply (false)
    id 'io.spring.dependency-management' version '1.1.4'
	id 'org.sonarqube' version '2.7'
	id 'maven-publish'
	id 'java-library'
	id 'signing'
}

group = 'pro.axenix-innovation'

sourceCompatibility = '17'
targetCompatibility = '17'

compileJava.options.encoding = 'UTF-8'
tasks.withType(Javadoc) { options.encoding = "UTF-8" }


jar {
    manifest {
        attributes 'Version': archiveVersion,
                'Build-time': new Date().format("dd-MM-yyyy'T'HH:mm:ss")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects {
    version = rootProject.version
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


dependencyManagement {
	imports {
		mavenBom(SpringBootPlugin.BOM_COORDINATES)
	}
}

dependencies {
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.amqp:spring-rabbit'
    implementation 'org.springframework:spring-web:6.1.3'

	// jms
	implementation 'org.springframework:spring-jms'
	implementation 'jakarta.jms:jakarta.jms-api'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // AspectJ
    implementation 'org.aspectj:aspectjweaver'

    // swagger annotations
    implementation 'org.springdoc:springdoc-openapi-starter-common:2.3.0'

    // code generation
    implementation 'com.squareup:javapoet:1.13.0'

    api 'jakarta.servlet:jakarta.servlet-api:6.1.0-M1'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

java {
    withJavadocJar()
    withSourcesJar()
}

String nexus_user = System.getenv('NEXUS_USER')
String nexus_password = System.getenv('NEXUS_PASSWORD')
String ossrhToken = System.getenv('ossrhToken')
String ossrhTokenPassword = System.getenv('ossrhTokenPassword')


String repoURL = ""
String sonatypeRepoURL = ""

if (version.endsWith('-SNAPSHOT')) {
    repoURL = System.getenv('NEXUS_SERVER_URL_SNAPSHOT').toString()
    sonatypeRepoURL = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
} else {
    repoURL = System.getenv('NEXUS_SERVER_URL_RELEASE').toString()
    sonatypeRepoURL = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.name
            groupId project.group
            version version
            from components.java
            pom {
                name = 'AxenAPI'
                description = 'Library for generation Spring conrollers by consumers (kafka consumers and other) with Swagger annotations. After that you can use it in your project and use Swagger UI.'
                url = 'https://github.com/AxenAPI/axenapi-library'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "axenix-innovation"
                        name = "AxenAPI-Generator Contributors"
                        email = "axenapi-info@axenix.pro"
                    }
                    scm {
                        url = "https://github.com/AxenAPI/axenapi-library"
                        connection = "scm:git:git://github.com/AxenAPI/axenapi-library.git"
                        developerConnection = "scm:git:ssh://git@github.com:AxenAPI/axenapi-library.git"
                    }
                    issueManagement {
                        system = "GitHub"
                        url = "https://github.com/AxenAPI/axenapi-library/issues"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            allowInsecureProtocol = true
            url repoURL
            credentials.setUsername(nexus_user)
            credentials.setPassword(nexus_password)
        }
        maven {
            allowInsecureProtocol = true
            url sonatypeRepoURL
            credentials {
                username = ossrhToken
                password = ossrhTokenPassword
            }
        }
    }
}